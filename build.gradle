buildscript {
  repositories {
    mavenCentral()
  }
}

plugins {
  id("io.spinnaker.plugin.bundler").version("$spinnakerGradleVersion")
  id("com.palantir.git-version").version("0.12.2")
  id("com.diffplug.spotless").version("5.1.0")
}

repositories {
  mavenCentral()
}

spinnakerBundle {
  pluginId = "Armory.EvaluateArtifactsPlugin"
  description = "An plugin that evaluates artifacts of a pipeline stage."
  provider = "https://github.com/armory-plugins"
  version = rootProject.version
}

version = normalizedVersion()

subprojects {
  group = "io.armory.plugin.stage.artifacts"
  version = rootProject.version

  if (name != "evaluate-artifacts-deck") {
    apply plugin: "com.diffplug.spotless"
    spotless {
      kotlin {
        ktlint().userData([
          disabled_rules          : "no-wildcard-imports",
          indent_size             : "2",
          continuation_indent_size: "2",
        ])
      }
    }
  }
}

String normalizedVersion() {
  String fullVersion = gitVersion()
  String normalized = fullVersion.split("-").first()
  if (fullVersion.contains("dirty")) {
    return "$normalized-SNAPSHOT"
  } else {
    return normalized
  }
}

